# Mainteiner: GrayJack <gr41.j4ck@gmail.com>

pkgname=cutter-radare
pkgver=1.2
pkgrel=1
pkgdesc="Qt and C++ GUI for radare2 reverse engineering framework"
_pkgradarever=2.3.0
url="https://github.com/radareorg/cutter"
arch=('x86_64')
license=('GPL3')
depends=('radare2' 'capstone' 'qt5-base' 'qt5-svg' 'icu')
makedepends=('git' 'qt5-tools')
provides=('cutter-radare')
conflicts=('radare2-cutter' 'radare2-cutter-git')
source=("https://github.com/radareorg/cutter/archive/v${pkgver}.tar.gz"
        "https://github.com/radare/radare2/archive/${_pkgradarever}.tar.gz")
sha256sums=('5b9e27dd54849744e9ac0075137fecabe4bed4e82ad220c2c0e0a348c0665783'
            '50a5797f168b10e02bd9c4d4ff61fe0459e1b79710c27405c94753694d6b9b3c')

prepare() {
    cp -R "${srcdir}/radare2-${_pkgradarever}/" "${srcdir}/radare2"
    mv "${srcdir}/radare2" "${srcdir}/cutter-${pkgver}/"
}

build() {
    cd "${srcdir}/cutter-${pkgver}/radare2/"
    ./sys/build.sh
    cd "${srcdir}/cutter-${pkgver}"
    mkdir build
    cd "build"
    qmake ../src
    make
}

package() {
    cd "${srcdir}/cutter-${pkgver}"
    install -DTm 755 build/cutter "${pkgdir}/usr/bin/cutter"
    install -DTm 644 src/cutter.desktop "${pkgdir}/usr/share/applications/cutter.desktop"
    install -DTm 644 src/img/cutter.svg "${pkgdir}/usr/share/icons/hicolor/scalable/apps/cutter.svg"
    install -dm755 docs/ "${pkgdir}/usr/share/doc/${pkgname}/"
    cp -a docs/* "${pkgdir}/usr/share/doc/${pkgname}/"
}
